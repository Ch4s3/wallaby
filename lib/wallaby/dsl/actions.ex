defmodule Wallaby.DSL.Actions do
  @moduledoc ~S"""
  Provides the action DSL.

  Actions are used to interact with form elements. They follow the same
  conventions as Wallaby.Node.Query. Form elements can be found based on
  their id, name, or label text:

  ```html
  <label for="first_name">
    First Name
  </label>
  <input id="user_first_name" type="text" name="first_name">
  ```

  ```
  fill_in(page, "First Name", with: "Grace")
  fill_in(page, "first_name", with: "Grace")
  fill_in(page, "user_first_name", with: "Grace")
  ```

  Note that the id selector does not need the `#`. This makes it easier to use
  with the ids generated by phoenix form helpers.

  There are several helpers for different interacting with different form elements.

  ```
  fill_in(page, "First Name", with: "Chris")
  choose(page, "Radio Button 1")
  check(page, "Checkbox")
  uncheck(page, "Checkbox")
  select(page, "My Awesome Select", option: "Option 1")
  click_on(page, "Some Button")
  attach_file(page, "Avatar", path: "test/fixtures/avatar.jpg")
  ```

  Actions return their parent node so that they can be chained together:

  ```
  page
  |> find(".signup-form")
  |> fill_in("Name", with: "Grace Hopper")
  |> fill_in("Email", with: "grace@hopper.com")
  |> click_on("Submit")
  ```
  """

  alias Wallaby.Node
  alias Wallaby.Drivable
  alias Wallaby.Driver

  @type parent :: Wallaby.Node.Query.parent
  @type locator :: Wallaby.Node.Query.locator
  @type opts :: Wallaby.Node.Query.opts

  @doc """
  Fills in a "fillable" node with text. Input nodes are looked up by id, label text,
  or name.
  """
  @spec fill_in(parent, locator, opts) :: parent

  def fill_in(parent, locator, [{:with, value} | _]=opts) when is_binary(value) do
    parent
    |> Node.Query.fillable_field(locator, opts)
    |> Node.fill_in(with: value)

    parent
  end
  def fill_in(parent, locator, [{:with, value} | _]=opts) when is_number(value) do
    fill_in(parent, locator,  Keyword.merge(opts, [with: to_string(value)]))
  end

  def fill_in(parent, with: value) when is_binary(value) do
    parent
    |> Node.fill_in(with: value)

    parent
  end

  def fill_in(parent, with: value) when is_number(value) do
    parent
    |> Node.fill_in(with: to_string(value))

    parent
  end

  @doc """
  Chooses a radio button based on id, label text, or name.
  """
  @spec choose(parent, locator, opts) :: parent

  def choose(parent, locator, opts\\[]) when is_binary(locator) do
    parent
    |> Node.Query.radio_button(locator, opts)
    |> Node.click

    parent
  end

  @doc """
  Checks a checkbox based on id, label text, or name.
  """
  @spec check(parent, locator, opts) :: parent

  def check(parent, locator, opts\\[]) do
    parent
    |> Node.Query.checkbox(locator, opts)
    |> Node.check

    parent
  end

  def check(parent) do
    parent
    |> Node.check

    parent
  end

  @doc """
  Unchecks a checkbox based on id, label text, or name.
  """
  @spec uncheck(parent, locator, opts) :: parent

  def uncheck(parent, locator, opts\\[]) do
    parent
    |> Node.Query.checkbox(locator, opts)
    |> Node.uncheck

    parent
  end

  def uncheck(parent) do
    Node.uncheck(parent)

    parent
  end

  @doc """
  Selects an option from a select box. The select box can be found by id, label
  text, or name. The option can be found by its text.
  """
  @spec select(parent, locator, option: String.t) :: parent

  def select(parent, locator, [option: option_text]=opts) do
    parent
    |> Node.Query.select(locator, opts)
    |> Node.Query.option(option_text, [])
    |> Node.click

    parent
  end

  @doc """
  Clicks the matching link. Links can be found based on id, name, or link text.
  """
  @spec click_link(parent, locator, opts) :: parent

  def click_link(parent, locator, opts\\[]) do
    parent
    |> Node.Query.link(locator, opts)
    |> Node.click

    parent
    |> Drivable.page
  end

  @doc """
  Clicks the matching button. Buttons can be found based on id, name, or button text.
  """
  @spec click_button(parent, locator, opts) :: parent

  def click_button(parent, locator, opts\\[]) do
    parent
    |> Node.Query.button(locator, opts)
    |> Node.click

    parent
  end

  @doc """
  Clicks on the matching button. Alias for `click_button`.
  """
  @spec click_on(parent, locator, opts) :: parent

  def click_on(parent, locator, opts\\[]) do
    click_button(parent, locator, opts)
  end

  # @doc """
  # Clears an input field. Input nodes are looked up by id, label text, or name.
  # The node can also be passed in directly.
  # """
  # @spec clear(Session.t, query) :: Session.t
  def clear(parent, locator, opts\\[]) do
    parent
    |> Node.Query.fillable_field(locator, opts)
    |> Node.clear()

    parent
  end

  def clear(parent) do
    parent
    |> Node.clear()

    parent
  end

  @doc """
  Attaches a file to a file input. Input nodes are looked up by id, label text,
  or name.
  """
  @spec attach_file(parent, locator, opts) :: parent

  def attach_file(parent, locator, [{:path, value} | _]=opts) do
    path = :filename.absname(value)

    parent
    |> Node.Query.file_field(locator, opts)
    |> Node.set(path)

    parent
  end

  def send_keys(session, keys) when is_list(keys) do
    Driver.send_keys(session, keys)
    session
  end

  @doc """
  Sends text characters to the active element
  """
  # @spec send_text(t, String.t) :: t

  def send_text(session, text) do
    Driver.send_text(session, text)
    session
  end
end
